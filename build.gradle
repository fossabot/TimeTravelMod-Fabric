plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
}

def apiVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'TimeTravelMod-Fabric'
group = 'com.rdvdev2.TimeTravelMod'

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:1.16.1"
    mappings "net.fabricmc:yarn:1.16.1+build.21:v2"
    modImplementation "net.fabricmc:fabric-loader:0.8.8+build.202"

    modImplementation "net.fabricmc.fabric-api:fabric-api:0.15.0+build.379-1.16.1"
    modImplementation "me.shedaniel.cloth:config-2:4.6.0"
    include "me.shedaniel.cloth:config-2:4.6.0"
    modImplementation "me.sargunvohra.mcmods:autoconfig1u:3.2.0-unstable"
    include "me.sargunvohra.mcmods:autoconfig1u:3.2.0-unstable"
    modImplementation "io.github.prospector:modmenu:1.14.5+build.30"

    implementation 'org.jetbrains:annotations:15.0'
}

processResources {
    def versionPropsFile = file('version.properties')
    inputs.file versionPropsFile

    Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))

    def mc = versionProps['MC']
    def majormod = versionProps['MAJORMOD']
    def majorapi = versionProps['MAJORAPI']
    def minor = versionProps['MINOR']
    def patch = versionProps['PATCH']

    apiVersion = mc + "-" + majormod + "." + majorapi
    project.version = apiVersion + "." + minor + "." + patch

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes(["Specification-Title": "Fabric Loader",
                    "Specification-Vendor": "The Fabric Loader Team",
                    "Specification-Version": "0.4.0",
                    "Implementation-Title": "Time Travel Mod",
                    "Implementation-Version": project.version,
                    "Implementation-Vendor" :"rdvdev2",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
    from "LICENSE"
}
